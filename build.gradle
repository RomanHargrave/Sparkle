apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'signing'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    compile     group: 'org.scala-lang',    name: 'scala-library-all',      version: '2.12.+'
    testCompile group: 'org.scalatest',     name: 'scalatest_2.12',     version: '2.2.5'
    testRuntime group: 'org.pegdown',       name: 'pegdown',            version: '1.1.0'
}

task scaladocJar(type: Jar) {
    classifier = 'javadoc'
    from scaladoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
/*
 * Publishing
 */

group               = 'info.hargrave'
archivesBaseName    = 'sparkle_2.12'
version             = '1'

ext {
    publish_username    = hasProperty('sonatype_username') ? sonatype_username : System.getenv('GRADLE_PUBLISH_USERNAME')
    publish_password    = hasProperty('sonatype_password') ? sonatype_password : System.getenv('GRADLE_PUBLISH_PASSWORD')
}

artifacts {
    archives jar, scaladocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }


            repository (url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publish_username, password: publish_password)
            }

            snapshotRepository (url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publish_username, password: publish_password)
            }

            pom.project {
                name        'Sparkle'
                packaging   'jar'
                description 'A concise DSL for XML built with scala.xml'
                url         'https://github.com/romanhargrave/sparkle'

                scm {
                    connection          'scm:git:git://github.com/romanhargrave/sparkle.git'
                    developerConnection 'scm:git:ssh://git@github.com/romanhargrave/sparkle.git'
                    url                 'https://github.com/romanhargrave/sparkle'
                }

                licenses {
                    license {
                        name    'The Apache Software License, Version 2.0'
                        url     'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id      'rhargrave'
                        name    'Roman Hargrave'
                        email   'roman@hargrave.info'
                    }
                }
            }
        }
    }
}
